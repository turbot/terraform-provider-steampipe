---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "steampipecloud_connection Resource - terraform-provider-steampipecloud"
subcategory: ""
description: |-
  Provides a Steampipe Connection resource. The `Steampipe Cloud Connection` represents a set of tables for a single data source. Each connection is represented as a distinct Postgres schema. In order to query data, you'll need at least one connection.

  Connections are defined at the user account or organization level, and they can be shared by multiple workspaces within the account or organization.
---

# Resource: steampipecloud_connection

Manages a connection, which is defined at the user account or organization level.

## Example Usage

**Create a user connection**

```hcl
resource "steampipecloud_connection" "test" {
  plugin     = "aws"
  handle     = "aws_aaa"
  access_key = "redacted"
  secret_key = "redacted"
  regions    = ["us-east-1"]
}
```

**Create an organization connection**

```hcl
resource "steampipecloud_connection" "aws_aaa" {
  organization = "myorg"
  plugin       = "aws"
  handle       = "aws_aaa"
  access_key   = "redacted"
  secret_key   = "redacted"
  regions      = ["us-east-1"]
}

resource "steampipecloud_connection" "aws_aab" {
  organization = "myorg"
  plugin       = "aws"
  handle       = "aws_aab"
  access_key   = "redacted"
  secret_key   = "redacted"
  regions      = ["us-east-1"]
}
```

## Argument Reference

The following arguments are supported:

- `handle` - (Required) A friendly identifier for your connection, and must be unique across your connections.
- `plugin` - (Required) The name of the plugin.
- `organization` - (Optional) An organization ID or handle to create the connection in.

### Airtable Connection Arguments

- `database_id` - Airtable database ID.
- `token` - Airtable API key. You can generate an API key by visiting [Creating a read-only API key](https://support.airtable.com/hc/en-us/articles/360056249614-Creating-a-read-only-API-key).
- `tables` - Names of the tables in the database, e.g., `["animals", "dogs", "cats"]`.

### Alicloud Connection Arguments

- `access_key` - Alicloud access key.
- `secret_key` - Alicloud secret key.
- `regions` - List of Alicloud regions to query, e.g., `["ap-south-1", "us-east-1"]`.

### AWS Access Key Mode Connection Arguments

- `access_key` - AWS access key.
- `secret_key` - AWS secret key.
- `session_token` - AWS temporary session token.
- `regions` - List of AWS regions to query, e.g., `["us-east-1", "us-east-2"]`.

### AWS Role Mode Connection Arguments

- `role_arn` - ARN of the IAM role to assume.
- `external_id` - External ID that the role trusts.
- `regions` - List of AWS regions to query, e.g., `["us-east-1", "us-east-2"]`.

#### Usage

This example requires the AWS provider, but if you have an existing role, you
can just create the `steampipecloud_connection` resource.

```hcl
data "steampipecloud_user" "caller" {}

resource "random_string" "random" {
  length  = 8
  special = false
  upper   = false
}

locals {
  # The format of external id is ^(u|o)_[a-z0-9_]{20}:[a-z0-9]{8}$`
  external_id = "${data.steampipecloud_user.caller.user_id}:${random_string.random.id}"
}


provider "aws" {
  region = "us-east-1"
}

# Create AWS IAM role with assume role policy
resource "aws_iam_role" "steampipe_cloud_role" {
  name = "steampipe_cloud"
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          AWS = "arn:aws:iam::123456789012:root"
        },
        Condition = {
          StringEquals = {
            "sts:ExternalId" = local.external_id
          }
        }
      },
    ]
  })
}

data "aws_iam_policy" "ReadOnlyAccess" {
  arn = "arn:aws:iam::aws:policy/ReadOnlyAccess"
}

# Attach ReadOnlyAccess policy to role
resource "aws_iam_role_policy_attachment" "steampipe_cloud_role_attach" {
  role       = aws_iam_role.steampipe_cloud_role.name
  policy_arn = data.aws_iam_policy.ReadOnlyAccess.arn
}

# Create connection with IAM role
resource "steampipecloud_connection" "aws_role_connection" {
  handle      = "aws_role_aaa"
  plugin      = "aws"
  regions     = ["us-east-1", "us-east-2"]
  role_arn    = aws_iam_role.steampipe_cloud_role.arn
  external_id = local.external_id
}
```

### Azure Connection Arguments

- `environment` - The Azure cloud environment to use, defaults to `AZUREPUBLICCLOUD`. Valid values are `AZUREPUBLICCLOUD`, `AZURECHINACLOUD`, `AZUREGERMANCLOUD`, `AZUREUSGOVERNMENTCLOUD`.
- `tenant_id` - Azure Active Directory tenant ID.
- `subscription_id` - Azure subscription ID.
- `client_id` - Azure client ID, also known as application ID.
- `client_secret` - Azure client secret, also known as client secret.

### Azure AD Connection Arguments

- `environment` - The Azure cloud environment to use, defaults to `AZUREPUBLICCLOUD`. Valid values are `AZUREPUBLICCLOUD`, `AZURECHINACLOUD`, `AZUREGERMANCLOUD`, `AZUREUSGOVERNMENTCLOUD`.
- `tenant_id` - Azure Active Directory tenant ID.
- `client_id` - Azure client ID, also known as application ID.
- `client_secret` - Azure client secret, also known as client secret.

### Bitbucket Connection Arguments

- `username` - Bitbucket username.
- `password` - Bitbucket app password.
- `base_url` - Base URL of Bitbucket instance.

### Cloudflare Connection Arguments

- `token` - Cloudflare API token.

### DigitalOcean Connection Arguments

- `token` - Personal access token for your DigitalOcean account.

### GCP Connection Arguments

- `project` - GCP project to query.
- `credentials` - The path to a JSON credential file or the contents of a service account key file in JSON format.

#### Usage

```hcl
resource "steampipecloud_connection" "gcp_aaa" {
  handle      = "gcp_aaa"
  plugin      = "gcp"
  project     = "project-aaa"
  credentials = file("/Users/myuser/Downloads/project-aaa.json")
}
```

### GitHub Connection Arguments

- `token` - GitHub personal access token used to authenticate.

### Hacker News Connection Arguments

- `max_items` (Number) The maximum number of items to be returned.

### IBM Connection Arguments

- `api_key` - IBM Cloud API key.
- `regions` - IBM Cloud regions to query, e.g., `["us-south", "eu-de"]`.

### Jira Connection Arguments

- `username` - Username used to access the API.
- `token` - Jira access token.
- `base_url` - Base URL of Jira instance.

### Linode Connection Arguments

- `token` - Linode API token. Please see [here](https://www.linode.com/docs/guides/getting-started-with-the-linode-api/) for more information.

### OCI Connection Arguments

- `user_ocid` - OCID of the user.
- `fingerprint` - Fingerprint of the key.
- `tenancy_ocid` - Tenancy's OCID.
- `private_key` - Path to the downloaded private key.
- `regions` - OCI regions to query, e.g., `["ap-mumbai-1", "us-ashburn-1"]`.

#### Usage

```hcl
resource "steampipecloud_connection" "oci_aaa" {
  handle       = "oci"
  plugin       = "oci"
  user_ocid    = "ocid1.user.oc1..aaaaaaaaw...."
  fingerprint  = "f1:fc:44:3a:.............."
  tenancy_ocid = "ocid1.tenancy.oc1..aaaaaaaah......"
  regions      = ["ap-mumbai-1", "us-ashburn-1"]
  private_key  = file("/Users/myuser/Downloads/mykey.cer")
}
```

### Slack Connection Arguments

- `token` - Slack application token.

### Stripe Connection Arguments

- `api_key` - Stripe API key. You can generate your API key by visiting [here](https://stripe.com/docs/keys).

### Twitter Connection Arguments

- `bearer_token` - OAuth 2.0 bearer token used to access publicly available information.

### Zendesk Connection Arguments

- `subdomain` - Organization subdomain name of your Zendesk instance.
- `email` - Email address of the agent.
- `api_key` - Zendesk API token.

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

- `connection_id` - An unique identifier of the connection.
- `identity_id` - A unique identifier of the entity where the connection is created.
- `type` - The type of the resource.

## Import

Connections can be imported using the `handle`, e.g.,

```sh
terraform import steampipecloud_connection.example aws_aaa
```
